let StartLogs = 
    traces
    | where timestamp >= ago(1d)
    | where message has "Running task"
    | extend 
        ServiceName = extract(@"Running task (\w+)", 1, message, typeof(string)),
        StartTime = timestamp
    | project ServiceName, StartTime;

let EndLogs = 
    traces
    | where timestamp >= ago(1d)
    | where message has "Task " and message has " completed"
    | extend 
        ServiceName = extract(@"Task (\w+) completed", 1, message, typeof(string)),
        EndTime = timestamp
    | project ServiceName, EndTime;

StartLogs
| join kind=leftouter (
    EndLogs
) on ServiceName
| where EndTime >= StartTime or isnull(EndTime)
| summarize EndTime = minif(EndTime, EndTime >= StartTime) by ServiceName, StartTime
| order by ServiceName asc, StartTime asc
